def projectName = 'NucleusFramework'
def majorVersion = '0'
def minorVersion = '0'
def buildBy = null
def mavenRepositories = [
        'https://github.com/JCThePants/mvn-repo/raw/master',
        'https://hub.spigotmc.org/nexus/content/repositories/snapshots/',
        'https://oss.sonatype.org/content/groups/public/'
]
def providedDependsFiles = null
def providedDepends = [
        'org.spigotmc:spigot-api:1.8-R0.1-SNAPSHOT',
        'com.sk89q:WorldEdit:SNAPSHOT',
        'net.milkbowl.vault:Vault:1.4.1',
        'com.google.code.findbugs:jsr305:3.0.0'
]
def compileDependsFiles = null
def compileDepends = null
def includeFiles = [
        'plugin.yml',
        'LICENSE.txt'
]
def testIncludes = [
        'com/jcwhatever/nucleus/TestAll.class'
]
def testExcludes = null
def testDepends = [
        'junit:junit:4.+',
        'org.spigotmc:unmapped-spigot:1.8-R0.1-SNAPSHOT'
]
def testRuntimeDepends = [
        'junit:junit:4.+'
]
def javaVersion = 1.7

/* Maven Repository Task Stuff */
def shCmd = 'sh'
def mavenCmd = 'mvn'
def mavenFolder = '/mvn-repo/'
def mavenGroupId = 'com.jcwhatever.bukkit'
def mavenArtifactId = projectName
def mavenVersion = majorVersion + '.' + minorVersion + '-SNAPSHOT'

/* Begin */

def hasTeamCity = hasProperty("teamcity")
def buildNumber = hasTeamCity ? teamcity["build.number"] : 'unknown'
// Set version number
if (hasTeamCity) {
    version = majorVersion + '.' + minorVersion + '.' + buildNumber + '.git-' + teamcity["build.vcs.number"]

    if (buildBy == null) {
        buildBy = teamcity["build.triggeredBy.username"]

        if (!buildBy) {
            buildBy = teamcity["build.triggeredBy"]
        }

        if (!buildBy) {
            buildBy = teamcity["agent.name"]
        }
    }

}else {
    version = majorVersion + '.' + minorVersion + '.000.git-unknown'
}

if (buildBy == null) {
    buildBy = System.properties['user.name']
}

defaultTasks 'build'

apply plugin: 'java'
compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

// Minimum Java Version
sourceCompatibility = javaVersion
targetCompatibility = javaVersion

// Add provided configuration to prevent dependencies
// from being included in the jar
configurations {
    provided
}

sourceSets {

    main {
        compileClasspath += configurations.provided
        runtimeClasspath += configurations.provided

        java {
            srcDir 'src'
        }
    }

    test {

        compileClasspath += configurations.provided
        runtimeClasspath += configurations.provided

        java {
            srcDir 'tests'
        }
    }
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies { classpath 'org.ajoberstar:gradle-git:0.2.3' }
}

repositories {
    mavenCentral()

    for (repo in mavenRepositories) {
        maven {
            url repo
        }
    }
}

// Add dependencies
dependencies {

    if (compileDependsFiles) {
        compile files(compileDependsFiles)
    }

    if (providedDependsFiles) {
        provided files(providedDependsFiles)
    }

    if (compileDepends) {
        compile compileDepends
    }

    if (providedDepends) {
        provided providedDepends
    }

    if (testDepends) {
        testCompile testDepends
    }

    if (testRuntimeDepends) {
        testRuntime testRuntimeDepends
    }
}

def manifestAttr = manifest {
    attributes("Built-By": buildBy,
            "Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
            "Implementation-Title": projectName,
            "Implementation-Version": version)
}

// Jar file output with version number
jar {

    archiveName = projectName + '.' + version + '.jar'

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }

    if (includeFiles) {
        from includeFiles
    }

    manifest = manifestAttr
}

/**
 * JUnit Tests
 */
test {
    if (testIncludes)
        include testIncludes

    if (testExcludes)
        exclude testExcludes

    testLogging {
        events 'started', 'passed'
    }

    minHeapSize = '128m'
    maxHeapSize = '512m'

    // set JVM arguments for the test JVM(s)
    jvmArgs '-XX:MaxPermSize=256m'

    // listen to events in the test execution lifecycle
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }

    onOutput { descriptor, event ->
        logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
    }
}

// Task to create a Jar file artifact with no version number
task simpleNamedJar(type: Jar) {

    archiveName = projectName + '.jar'

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }

    if (includeFiles) {
        from includeFiles
    }

    manifest = manifestAttr

    with jar
}

artifacts {
    archives simpleNamedJar
}

/* Maven Snapshot Update Tasks */
import org.ajoberstar.gradle.git.tasks.*
task cloneMaven(type: GitClone) {
    def destination = file(mavenFolder)
    uri = "https://github.com/JCThePants/mvn-repo.git"
    destinationPath = destination
    bare = false
    enabled = !destination.exists()
}

task updateLocalMavenHolding(type: Copy) {
    from file('build/libs/' + projectName + '.jar')
    into file(mavenFolder + 'holding')
}

task updateLocalMaven(type: Exec) {
    workingDir mavenFolder
    executable shCmd
    args '--login', '-i', '-c', mavenCmd + ' install:install-file -DgroupId=' + mavenGroupId +
            ' -DartifactId=' + mavenArtifactId + ' -Dversion=' + mavenVersion + ' -Dfile=holding/' + projectName + '.jar -Dpackaging=jar -DgeneratePom=true -DlocalRepositoryPath=';
}

task addMavenGit(type: Exec) {
    workingDir mavenFolder
    executable shCmd
    args '--login', '-i', '-c', 'git add .';
}

task commitMavenGit(type: Exec) {
    workingDir mavenFolder
    executable shCmd


    args '--login', '-i', '-c', "git commit -m 'Snapshot update for " + projectName + " build# " + buildNumber + "'";
}

task pushMavenGit(type: Exec) {
    workingDir mavenFolder
    executable shCmd
    args '--login', '-i', '-c', "git push GITHUB master";
}

task updateMaven << {
    updateLocalMavenHolding.execute()
    updateLocalMaven.execute()
    addMavenGit.execute()
    commitMavenGit.execute()
    pushMavenGit.execute()
}